buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply from: '../dependencies.gradle'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.bankex.pay"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION

        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName computeVersionName(versionCode)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
        }

        develop {
            applicationIdSuffix ".dev"
            minifyEnabled false
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file
        // as noted by @Vishnuvathsan you may also need to include
        // variations on the file name. It depends on your dependencies.
        // Some other common variations on notice and license file names
        //exclude 'META-INF/notice'
        //exclude 'META-INF/notice.txt'
        //exclude 'META-INF/license'
        //exclude 'META-INF/license.txt'
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

def computeVersionName(int versionCode) {
    if (VERSION_HOTFIX > 0) {
        return String.format('%d.%d.%d', VERSION_MAJOR, VERSION_MINOR, VERSION_HOTFIX)
    } else {
        return String.format('%d.%d', VERSION_MAJOR, VERSION_MINOR)
    }
}

project.ext {
    retrofitVersion = "2.3.0"
    okhttpVersion = "3.9.0"
    supportVersion = "27.0.2"
    web3jVersion = "3.1.1-android"
    gethVersion = "1.7.3"
    gsonVersion = "2.8.2"
    rxJavaVersion = "2.1.6"
    rxAndroidVersion = "2.0.1"
    daggerVersion = "2.11"
    butterknifeVersion = '8.8.1'
}

dependencies {

    // Etherium client
    implementation "org.web3j:core:$project.web3jVersion"
    implementation "org.ethereum:geth:$project.gethVersion"

    // Http client
    implementation "com.squareup.retrofit2:retrofit:$project.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$project.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$project.retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$project.okhttpVersion"
    implementation "com.google.code.gson:gson:$project.gsonVersion"
    implementation 'android.arch.lifecycle:runtime:1.0.3'
    implementation 'android.arch.lifecycle:extensions:1.0.0'
    implementation "com.android.support:appcompat-v7:$project.supportVersion"
    implementation "com.android.support:design:$project.supportVersion"
    implementation "com.android.support:support-vector-drawable:$project.supportVersion"
    implementation "com.android.support:recyclerview-v7:$project.supportVersion"
    implementation 'com.android.support:multidex:1.0.2'

    // Bar code scanning
    implementation 'com.google.zxing:core:3.2.1'
    implementation 'com.google.android.gms:play-services-vision:11.8.0'

    // Sugar
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.apl-devs:appintro:v4.2.2'
    implementation 'com.romandanylyk:pageindicatorview:1.0.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.2.0@aar'
    implementation "com.jakewharton:butterknife:$butterknifeVersion"

    // ReactiveX
    implementation "io.reactivex.rxjava2:rxjava:$project.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$project.rxAndroidVersion"

    // Dagger 2

    // Dagger core
    implementation "com.google.dagger:dagger:$project.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$project.daggerVersion"

    // Dagger Android
    implementation "com.google.dagger:dagger-android-support:$project.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$project.daggerVersion"

    // if you are not using support library, include this instead
    implementation "com.google.dagger:dagger-android:$project.daggerVersion"

    //CreateWalletLibs
    implementation 'io.github.novacrypto:BIP39:0.1.9'
    implementation 'com.github.Plumillon:ChipView:1.2.0'

    implementation 'com.elegion.littlefinger:littlefinger:0.9.0'

    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    androidTestImplementation('tools.fastlane:screengrab:1.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true;
    }

    // PW
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':tn')
}
